# Pose & Groom example
# pose defines an interface (method signatures).
# groom adds methods to kitty/collar types.
# Types structurally satisfy a pose if they have all required methods.

# Define an interface
pose Showable {
    meow show() string
}

# Kitty definitions
kitty Cat {
    name: string,
    age: int
}

kitty Dog {
    name: string,
    good_boy: bool
}

# Groom: add methods to Cat
groom Cat {
    meow show() string {
        bring self.name + " (age " + to_string(self.age) + ")"
    }
    meow is_kitten() bool {
        bring self.age < 1
    }
}

# Groom: add methods to Dog
groom Dog {
    meow show() string {
        sniff (self.good_boy) {
            bring self.name + " (good boy!)"
        }
        bring self.name
    }
}

# Use methods on kitty instances
nyan nyantyu = Cat("Nyantyu", 3)
nyan tyako = Cat("Tyako", 0)
nyan tyomusuke = Dog("Tyomusuke", yarn)

nya(nyantyu.show())
nya(tyako.show())
nya(tyomusuke.show())

# Boolean method
sniff (tyako.is_kitten()) {
    nya(tyako.name + " is a kitten!")
}

# Collar with methods
collar Label = string

groom Label {
    meow display() string {
        bring "[ " + self.value + " ]"
    }
}

nyan tag = Label("important")
nya(tag.display())
